#!/usr/bin/env bash

# `.competions` handles all custom auto-completions.


# === fzf-tab ===
# https://github.com/Aloxaf/fzf-tab

# enable hidden files on completion
zstyle ':completion:*' special-dirs true
# hide parents
zstyle ':completion:*' ignored-patterns '.|..|.DS_Store|**/.|**/..|**/.DS_Store|**/.git'
# hide `..` and `.` from file menu
zstyle ':completion:*' ignore-parents 'parent pwd directory'
# set descriptions format to enable group support
zstyle ':completion:*:descriptions' format '[%d]'
# force zsh not to show completion menu,
# which allows fzf-tab to capture the unambiguous prefix
zstyle ':completion:*' menu no

# switch groups using `[` and `]`
zstyle ':fzf-tab:*' switch-group '[' ']'
# fzf-tab theme, setting the default color for suggestions (blue for me)
# Test all colors: `msgcat --color=test`
zstyle ':fzf-tab:*' default-color $'\x1b[30m'
zstyle ':fzf-tab:*' fzf-flags '--color=hl:#5f87af'

# Tools to autocomplete:
# ----------------------
# Big source of inspiration:
# https://github.com/Freed-Wu/fzf-tab-source

# complete `ls` / `cat` / etc
zstyle ':fzf-tab:complete:(\\|*/|)(ls|gls|bat|cat|cd):*' fzf-preview \
  '_fzf_complete_realpath "$realpath"'

# complete `make`
zstyle ':fzf-tab:complete:(\\|*/|)make:*' fzf-preview \
  'case "$group" in
  "[make target]")
    make -n "$word" | bat -lsh
    ;;
  "[make variable]")
    make -pq | ag "^$word =" | bat -lsh
    ;;
  "[file]")
    _fzf_complete_realpath "$realpath"
    ;;
  esac'
