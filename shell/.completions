#!/usr/bin/env bash

# `.competions` handles all custom auto-completions.


# === fzf-tab ===
# https://github.com/Aloxaf/fzf-tab

# enable hidden files on completion
zstyle ':completion:*' special-dirs true
# hide parents
zstyle ':completion:*' ignored-patterns '.|..|.DS_Store|**/.|**/..|**/.DS_Store|**/.git'
# hide `..` and `.` from file menu
zstyle ':completion:*' ignore-parents 'parent pwd directory'
# force zsh not to show completion menu,
# which allows fzf-tab to capture the unambiguous prefix
zstyle ':completion:*' menu no

# switch groups using `[` and `]`
zstyle ':fzf-tab:*' switch-group '[' ']'

# use the same layout as others and respect my default
local fzf_flags
zstyle -a ':fzf-tab:*' fzf-flags fzf_flags
fzf_flags=( "${fzf_flags[@]}" '--layout=reverse-list' )
zstyle ':fzf-tab:*' fzf-flags $fzf_flags

# Tools to autocomplete:
# ----------------------
# Big source of inspiration:
# https://github.com/Freed-Wu/fzf-tab-source

# complete `ls` / `cat` / etc
zstyle ':fzf-tab:complete:(\\|*/|)(ls|gls|bat|cat|cd):*' fzf-preview \
  '_fzf_complete_realpath "$realpath"'

# complete `make`
zstyle ':fzf-tab:complete:(\\|*/|)make:*' fzf-preview \
  'case "$group" in
  "[make target]")
    make -n "$word" | bat -lsh --theme=$SOBOLE_SYNTAX_THEME
    ;;
  "[make variable]")
    make -pq | ag "^$word =" | bat -lsh --theme=$SOBOLE_SYNTAX_THEME
    ;;
  "[file]")
    _fzf_complete_realpath "$realpath"
    ;;
  esac'
